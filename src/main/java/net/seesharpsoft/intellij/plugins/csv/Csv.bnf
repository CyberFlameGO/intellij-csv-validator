{
  parserClass="net.seesharpsoft.intellij.plugins.csv.parser.CsvParser"

  parserImports=["static net.seesharpsoft.intellij.plugins.csv.parser.CsvParserUtil.*"]

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Csv"
  psiImplClassSuffix="Impl"
  psiPackage="net.seesharpsoft.intellij.plugins.csv.psi"
  psiImplPackage="net.seesharpsoft.intellij.plugins.csv.psi.impl"

  elementTypeHolderClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvTypes"
  elementTypeClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvElementType"
  tokenTypeClass="net.seesharpsoft.intellij.plugins.csv.psi.CsvTokenType"

  tokens=[
    TEXT='regexp:[^ ,;\t\r\n"]+'
    ESCAPED_TEXT='regexp:([,;\t\r\n]|"")+'
    COMMA='regexp:[,;\t]'
    QUOTE='"'
    CRLF='regexp:\n'
   ]
}

csvFile ::= record (CRLF record)* [CRLF]

record ::= field ( << separator >> COMMA field)*

field ::= (escaped | nonEscaped)

private escaped ::= QUOTE ( TEXT | ESCAPED_TEXT)* QUOTE

private nonEscaped ::= TEXT*